%new-style
%strict-args
%require-types
%enable-all-warnings

%requires ExcelDataProvider

class BBM_ExcelReadDataProvider {
    public {
        # map config item names to data provider options
        const KeyMap = map {
            ("excel-read-" + regex_subst($1, "_", "-", RE_Global)): $1,
        }, keys ExcelReadDataProvider::ConstructorOptions;
    }

    #! alias for searchRecordsConnector()
    static *list<hash<auto>> searchRecords(auto input) {
        return BBM_ExcelReadDataProvider::searchRecordsConnector(input);
    }

    #! Search records connector
    /**
        @param input available in the \a input key of local context data (i.e. <tt>$local:input</tt> when looking up
        config item values)

        @return output data: list of record hashes
    */
    static *list<hash<auto>> searchRecordsConnector(auto input) {
        hash<auto> local_context = {"input": input};
        ExcelWorksheetReadDataProvider prov(BBM_ExcelReadDataProvider::getOptions(local_context));
        return map $1, prov.searchRecords(
            UserApi::getConfigItemValue("excel-read-search-where"),
            UserApi::getConfigItemValue("excel-read-search-options"),
        );
    }

    static hash<auto> getOptions(*hash<auto> local_context) {
        # map from config item names to data provider names in the option hash; remove empty values
        hash<auto> opts = map {$1.value: UserApi::getConfigItemValue($1.key, local_context)}, KeyMap.pairIterator();
        # remove null options
        map remove opts{$1.key}, opts.pairIterator(), !exists $1.value;
        if (!opts.path && !opts.stream) {
            throw "EXCEL-READ-ERROR", sprintf("either the 'excel-read-path' or 'excel-read-stream' option is "
                "required; options provided: %y", keys opts);
        }
        UserApi::logInfo("excel read options: %y", opts);
        return opts;
    }
}
