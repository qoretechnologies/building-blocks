# name: BBM_RemoteDb2LocalDbImportWithRecovery
# version: 1.0
# desc: building block base step for high-performance remote DB -> local DB data transfers supporting recovery
# author: Qore Technologies, s.r.o.
# requires: BBM_RemoteDb2LocalDbImportBase
%new-style
%require-types
%strict-args
%enable-all-warnings

#! transfers data from a remote DB to a local DB
class BBM_RemoteDb2LocalDbImportWithRecovery inherits BBM_RemoteDb2LocalDbImportBase {
    constructor(*string label) : BBM_RemoteDb2LocalDbImportBase(label) {
    }

    #! returns True if local data is present, False if not
    bool checkLocal() {
        # get mapper name
        string mapper_name = UserApi::getConfigItemValue("remote2local-db-mapper-name");
        Mapper mapper = cast<InboundTableMapper>(UserApi::getMapper(mapper_name));
        AbstractDataProvider output_provider = mapper.getOutputProvider();

        string column_name = UserApi::getConfigItemValue("remote2local-db-recovery-column");
        auto value = UserApi::getConfigItemValue("remote2local-db-recovery-value-template");

        hash<auto> where_hash = {
            column_name: value,
        };

        *hash<auto> row = output_provider.searchSingleRecord(where_hash);
        if (row) {
            UserApi::logInfo("found data with %y = %y; transfer is already COMPLETE", column_name, value);
            return True;
        }

        UserApi::logInfo("no data found with %y = %y; transfer will be retried", column_name, value);
        return False;
    }

    private *hash<string, hash<ConfigItemInfo>> getConfigItems() {
        *string item_pfx_str;
        *string desc_pfx_str;
        if (item_pfx) {
            item_pfx_str = item_pfx + "-";
            desc_pfx_str = item_pfx[0].upr() + item_pfx[1..] + ": ";
        }

        return BBM_RemoteDb2LocalDbImportBase::getConfigItems() + {
            # recovery items
            item_pfx_str + "remote2local-db-recovery-column": <ConfigItemInfo>{
                "description": "the name of the column for recovery",
                "strictly_local": True,
                "config_group": desc_pfx_str + "Remote DB Import Recovery",
            },
            item_pfx_str + "remote2local-db-recovery-value-template": <ConfigItemInfo>{
                "description": "value for recovery",
                "strictly_local": True,
                "config_group": desc_pfx_str + "Remote DB Import Recovery",
            },
        };
    }
}
# END
