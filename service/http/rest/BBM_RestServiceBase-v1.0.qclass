# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%require-types
%strict-args
%enable-all-warnings

# import the Swagger module for Swagger / OpenAPI 2.0 REST schema validation
%requires Swagger

#! The base CORS-handling code
class BBM_CorsRest inherits BBM_CorsBase {
    #! Creates the object
    constructor() : BBM_CorsBase("rest") {
    }

    #! Returns a response to the OPTIONS method if CORS is enabled, otherwise returns an error
    hash<HttpHandlerResponseInfo> options(hash<auto> cx, *hash<auto> ah) {
        # check if CORS is enabled
        *hash<auto> hdr = getCorsResponseHeaders(cx);
        if (hdr) {
            # return a CORS response
            return RestHandler::makeResponse(200, NOTHING, hdr);
        }

        # see if alternate methods would work
        list<string> hl = map $1.key, RestHandler::Methods.pairIterator(),
            $1.key != "OPTIONS" && self.hasCallableMethod($1.value + cx.rest_method);
        # if we have alternate methods, return an appropriate 405 Method Not Allowed error
        if (hl) {
            string desc = sprintf("HTTP method %s is unimplemented in REST class %y (%s)", cx.hdr.method, name(),
                self.className());
            if (cx.orig_method)
                desc += sprintf(" REST method %y", cx.orig_method);
            string ml = foldl $1 + "," + $2, hl;
            desc += sprintf(", available methods: %s", ml);
            return cast<hash<HttpHandlerResponseInfo>>(
                HttpServer::AbstractHttpRequestHandler::makeResponse({"Allow": ml}, 405, desc)
            );
        }

        # otherwise return a 501 Not Implemeneted response
        return cast<hash<HttpHandlerResponseInfo>>(RestHandler::make501("REST class %y (%s) does not "
            "implement method %y", name(), self.className(), "options"));
    }

    #! Returns the current REST URI path element name
    abstract string name();
}

#! The base request handler class
class BBM_RestClass inherits AbstractRestClass, BBM_CorsRest {
    #! Helper method to process REST API arguments according to an option description hash
    static *hash<auto> doGetOptions(*hash<auto> ah, hash<auto> odesc) {
        # first check for extra args
        if (*hash<auto> extra = ah - (keys odesc)) {
            throw "REST-ARG-ERROR", sprintf("Unknown option(s) provided: %y (known options: %y)", keys extra,
                keys odesc);
        }
        hash<auto> opts;
        foreach hash<auto> i in (odesc.pairIterator()) {
            softlist<string> types;
            bool optional;
            auto default_value;
            switch (i.value.typeCode()) {
                case NT_STRING: {
                    types = i.value =~ /|/ ? i.value.split("|") : i.value;
                    break;
                }
                case NT_HASH: {
                    if (i.value.type.typeCode() == NT_STRING) {
                        types = i.value.type;
                    } else if (i.value.types.typeCode() == NT_LIST) {
                        types = i.value.types;
                    } else {
                        throw "OPTION-DESC-ERROR", sprintf("Invalid option description for option %y: %y (expecting "
                            "a hash with a \"type\" or \"types\" key)", i.key, i.value);
                    }
                    if (i.value.hasKey("default_value")) {
                        default_value = i.value.default_value;
                    }
                    break;
                }
                default:
                    throw "OPTION-DESC-ERROR", sprintf("Invalid option description for option %y: %y (expecting "
                        "\"string\" or \"hash\")", i.key, i.value);
            }
            if (exists default_value && !optional) {
                optional = True;
            }
            bool multi = types.size() > 1;
            bool ok;
            int ocount;
            foreach string type in (types) {
                *bool toptional = multi ? True : optional;
                if (type =~ /^\*/) {
                    if (!toptional) {
                        toptional = True;
                    }
                    type =~ s/^\*//;
                }
                if (toptional) {
                    ++ocount;
                }
                switch (type) {
                    case "string":
                        opts{i.key} = doGetString(ah, i.key, toptional, NOTHING, multi);
                        break;

                    case "bool":
                        opts{i.key} = doGetBool(ah, i.key, toptional, NOTHING, multi);
                        break;

                    case "int":
                        opts{i.key} = doGetInt(ah, i.key, toptional, NOTHING, multi);
                        break;

                    case "date":
                        opts{i.key} = doGetDate(ah, i.key, toptional, multi);
                        break;

                    case "list<string>":
                        opts{i.key} = doGetStringList(ah, i.key, toptional, multi);
                        break;

                    case "list<hash>":
                        opts{i.key} = doGetListOfHashes(ah, i.key, toptional, multi);
                        break;

                    case "list<int>":
                        opts{i.key} = doGetListOfInts(ah, i.key, toptional, multi);
                        break;

                    case "hash":
                        opts{i.key} = doGetHash(ah, i.key, toptional, NOTHING, multi);
                        break;

                    case "number":
                        opts{i.key} = doGetNumber(ah, i.key, toptional, multi);
                        break;
                    default:
                        throw "OPTION-DESC-ERROR", sprintf("Invalid option description for option %y: %y (unknown type)",
                            i.key, i.value);
                }
                if (exists opts{i.key}) {
                    break;
                }
            }
            if (!exists opts{i.key}) {
                if (exists default_value) {
                    opts{i.key} = default_value;
                } else if (!optional && (ocount != types.size())) {
                    throw "REST-ARG-ERROR", sprintf("Missing required option %y", i.key);
                }
            }
        }
        return opts;
    }

    #! helper method that helps acquiring int argument values
    static *int doGetInt(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_STRING:
                if (ah{key}.intp()) {
                    return ah{key}.toInt();
                }
                break;

            case NT_INT:
                return ah{key};
        }

        # will be converted to a 400 Bad Request response
        throw "REST-ARG-ERROR", sprintf("missing required int argument %y (got type %y)", key, ah{key}.fullType());
    }

    #! helper method that helps acquiring number argument values
    static *softnumber doGetNumber(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_STRING:
                if (ah{key}.intp()) {
                    return ah{key};
                }
                break;

            case NT_INT:
            case NT_FLOAT:
            case NT_NUMBER:
                return ah{key};
        }

        # will be converted to a 400 Bad Request response
        throw "REST-ARG-ERROR", sprintf("missing required number argument %y (got type %y)", key, ah{key}.fullType());
    }

    #! helper method that helps acquiring string argument values
    static *string doGetString(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_STRING:
                return ah{key};
        }

        # will be converted to a 400 Bad Request response
        throw "REST-ARG-ERROR", sprintf("missing required string argument %y (got type %y)", key, ah{key}.fullType());
    }

    #! helper method that helps acquiring arguments as lists of strings
    static *list<softstring> doGetStringList(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_STRING:
                # make sure and respect quoted strings to allow for processing file names with spaces
                return ah{key}.split(" ", "\"", True);

            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_LIST:
                return ah{key};
        }

        throw "REST-ARG-ERROR", sprintf("Missing or invalid required argument %y (got type %y; expecting "
            "\"string\" or \"list\")", key, ah{key}.type());
    }

    #! helper method that helps acquiring arguments as lists of hashes
    static *list<auto> doGetListOfHashes(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_HASH:
                return (ah{key},);

            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_LIST:
                return ah{key};
        }

        throw "REST-ARG-ERROR", sprintf("Missing or invalid required argument %y (got type %y; expecting "
            "\"list\")", key, ah{key}.type());
    }

    #! helper method that helps acquiring arguments as lists of integers
    static *list<auto> doGetListOfInts(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_INT:
                return (ah{key},);

            case NT_STRING:
                ah{key} = ah{key}.split(",");
                # fall down to next case

            case NT_LIST:
                foreach auto v in (\ah{key}) {
                    if (!v.intp()) {
                        throw "REST-ARG-ERROR", sprintf("Invalid list element %y (%d/%d starting from 1) in argument "
                            "%y (expected a type convertible to an int; got type %y instead)", v, $# + 1,
                            ah{key}.size(), key, ah{key}.fullType());
                    }
                    v = v.toInt();
                }
                return ah{key};
        }

        throw "REST-ARG-ERROR", sprintf("Missing or invalid required argument %y (got type %y; expecting "
            "\"list\")", key, ah{key}.type());
    }

    #! helper method that helps acquiring boolean argument values
    static *bool doGetBool(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_STRING:
                return parse_boolean(ah{key});

            case NT_FLOAT:
            case NT_INT:
            case NT_NUMBER:
                return boolean(ah{key});

            case NT_BOOLEAN:
                return ah{key};
        }

        # will be converted to a 400 Bad Request response
        throw "REST-ARG-ERROR", sprintf("missing required boolean argument %y (got type %y)", key,
            ah{key}.fullType());
    }

    #! helper method that helps acquiring date argument values
    static *date doGetDate(*hash<auto> ah, string key, *bool optional) {
        switch (ah{key}.typeCode()) {
            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_STRING:
                return date(ah{key});

            case NT_DATE:
                return ah{key};
        }

        # will be converted to a 400 Bad Request response
        throw "REST-ARG-ERROR", sprintf("missing required date argument %y (got type %y)", key, ah{key}.fullType());
    }

    static *hash<auto> doGetHash(*hash<auto> ah, string key, *bool optional, *string location) {
        switch (ah{key}.typeCode()) {
            case NT_STRING: {
                try {
                    auto v = parse_to_qore_value(ah{key});
                    if (v.typeCode() == NT_HASH) {
                        return v;
                    }
                } catch (hash<ExceptionInfo> ex) {
                }
                throw "REST-ARG-ERROR", sprintf("missing or invalid required argument %y%s (got type %y; expecting "
                    "\"hash\" or \"string\" that can be parsed to a hash)", key,
                    location ? sprintf(" in the %y option", location) : NOTHING, ah{key}.type());
            }

            case NT_NOTHING:
                if (optional) {
                    return;
                }
                break;

            case NT_HASH:
                return ah{key};
        }

        throw "REST-ARG-ERROR", sprintf("missing or invalid required argument %y%s (got type %y; expecting "
            "\"hash\" or \"string\" that can be parsed to a hash)", key,
            location ? sprintf(" in the %y option", location) : NOTHING, ah{key}.type());
    }
}

#! Attribute REST class
class BBM_AttributeRestClass inherits BBM_RestClass {
    public {
        const DefaultKeyRegexList = ("name$", "id$");
        const DefaultName = "attribute";
    }

    private {
        auto info;
        string key;
        softstring name;
    }

    constructor(auto info, *string key) {
        self.info = info;
        setName(info, key);
    }

    private setName(auto info, *string key) {
        if (info.strp()) {
            name = info;
            if (name.val()) {
                return;
            }
        }

        if (info.typeCode() == NT_HASH) {
            if (key.val() && info{key}.str()) {
                name = info{key};
                if (name.val()) {
                    return;
                }
            }

            string anykey;
            softstring anyname;

            # if no name is set, try to find an identifier in the hash data
            foreach hash<auto> i in (info.pairIterator()) {
                foreach string regex in (DefaultKeyRegexList) {
                    if (i.value.strp()) {
                        if (!anyname.val()) {
                            anykey = i.key;
                            anyname = i.value;
                        }
                        if (Qore::regex(i.key, regex, RE_Caseless)) {
                            name = i.value;
                            if (name.val()) {
                                key = i.key;
                                return;
                            }
                        }
                    }
                }
            }

            # if no name is set, take the value of the first field that can be converted to a string
            if (anyname.val()) {
                key = anykey;
                name = anyname;
                return;
            }
        }

        # if no name is set, set the default name
        name = DefaultName;
    }

    *AbstractRestClass subClassImpl(string name, hash<auto> cx, *hash<auto> ah) {
        if (info.typeCode() == NT_HASH) {
            if (info.hasKey(name)) {
                return new BBM_AttributeRestClass(info{name});
            }
        } else if (key.val() && info.typeCode() == NT_LIST && info[0].typeCode() == NT_HASH) {
            # try to find the element requested
            foreach hash<auto> h in (info) {
                if (h{key} == name) {
                    return new BBM_AttributeRestClass(h - key);
                }
            }
        }
    }

    softstring name() {
        return name;
    }

    hash<HttpHandlerResponseInfo> get(hash<auto> cx, *hash<auto> ah) {
        return RestHandler::makeResponse(200, info);
    }

    hash<HttpHandlerResponseInfo> put(hash<auto> cx, *hash<auto> ah) {
        return RestHandler::makeResponse(200, info);
    }
}

#! The base REST handler class
class BBM_RestHandlerBase inherits AbstractServiceRestHandler, BBM_HttpAuthenticatorBase, BBM_CorsRest {
    #! Creates the object
    constructor(AbstractAuthenticator auth = BBM_HttpAuthenticatorBase::getAuthenticator("rest-auth-name",
            "rest-auth-cookie", "rest-auth-redirect"))
            : AbstractServiceRestHandler(
            ServiceApi::getConfigItemValue("rest-root-uri"),
            False,
            auth,
            BBM_RestHandlerBase::getSwaggerSchema()) {
    }

    #! Returns the Swagger schema, if any
    private static *SwaggerSchema getSwaggerSchema() {
        *string schema = ServiceApi::getConfigItemValue("rest-swagger-schema");
        if (!schema) {
            return;
        }
        # add "resource://" prefix to string if it is not a location
        if (schema !~ /\w+:\/\//) {
            schema = "resource://" + schema;
        }
        return SwaggerLoader::fromString(UserApi::getTextFileFromLocation(schema));
    }

    hash<HttpResponseInfo> handleRequest(HttpListenerInterface listener, Socket s, hash<auto> cx, hash<auto> hdr, *data b) {
        hash<HttpResponseInfo> rv = AbstractServiceRestHandler::handleRequest(listener, s, cx, hdr, b);
        rv.hdr = getCorsResponseHeaders(cx) + rv.hdr;
        return rv;
    }

    removeRootPath(reference<string> path) {
        *int rm_comp = ServiceApi::getConfigItemValue("rest-remove-leading-path-components");
        if (!exists rm_comp && url.val()) {
            rm_comp = url.split("/").size();
        }
        if (rm_comp) {
            list<string> l = path.split("/");
            int start = 1;
            if (path !~ /^\//) {
                --start;
            }
            # leave any URI query in the path
            if (rm_comp == l.size()) {
                path =~ s/^[^\?]*//;
            } else {
                splice l, start, rm_comp;
                path = l.join("/");
            }
        }
    }

    string name() {
        return "/";
    }

    private hash<HttpHandlerResponseInfo> returnRestException(hash<ExceptionInfo> ex) {
        if (ex.err =~ /-ARG-ERROR$/) {
            return <HttpHandlerResponseInfo>{
                "code": 400,
                "body": ex.desc,
            };
        }
        if (ex.err == "AUTHORIZATION-ERROR" || ex.err =~ /-ACCESS-ERROR$/) {
            return <HttpHandlerResponseInfo>{
                "code": 403,
                "body": ex,
            };
        }
        if (ex.err == "REST-NOT-FOUND-ERROR") {
            return <HttpHandlerResponseInfo>{
                "code": 404,
                "body": ex.desc,
            };
        }
        # for all other errors, return the standard "409 Conflict" response
        return RestHandler::returnRestException(ex);
    }
}

#! The REST service base class; includes a parametrized handler class as well
class BBM_RestServiceBase inherits BBM_HttpServiceGenericBase {
}
